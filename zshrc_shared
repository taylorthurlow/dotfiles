source /usr/local/share/zsh-autosuggestions/zsh-autosuggestions.zsh
source $HOME/.oh-my-zsh/custom/plugins/zsh-histdb/sqlite-history.zsh
autoload -Uz add-zsh-hook
add-zsh-hook precmd histdb-update-outcome

bindkey '^n' autosuggest-accept
bindkey '^ ' autosuggest-execute

# Up a directory with "up"
up() {
	cd $(eval printf '../'%.0s {1..$1})
}

# Makedir and cd into it
mkcd() {
	if [ $# != 1 ]; then
		echo "Usage: mkcd <dir>"
	else
		mkdir -p $1 && cd $1
	fi
}

# Makedir and take ownership
mkchown() {
	if [ $# != 1 ]; then
		echo "Usage: mkchown <dir>"
	else
		sudo mkdir -p $1 && sudo chown $(whoami):$(whoami) $1
	fi
}

# Delete local and remote branches
function git-nuke {
	git branch -D $1 && git push origin :$1
}

# make git tab completion faster
__git_files () {
	_wanted files expl 'local files' _files
}

# easy kill processes with fzf
kill_process() {
	local pid=$(ps -ef | sed 1d | eval "fzf ${FZF_DEFAULT_OPTS} -m --header='[kill:process]'" | awk '{print $2}')

	if [ "x$pid" != "x" ]
	then
		echo $pid | xargs kill -${1:-9}
		kp
	fi
}

# brew install with fzf
brew_install_fzf() {
	local inst=$(brew search | eval "fzf ${FZF_DEFAULT_OPTS} -m --header='[brew:install]'")

	if [[ $inst ]]; then
		for prog in $(echo $inst)
		do brew install $prog
		done
	fi
}

# checkout git branch (including remote branches), sorted by most recent commit, limit 30 last branches
fbr() {
	local branches branch
	branches=$(git for-each-ref --count=30 --sort=-committerdate refs/heads/ --format="%(refname:short)") &&
	branch=$(echo "$branches" |
           fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
	git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}

# autojump when used with no args uses fzf
j() {
    if [[ "$#" -ne 0 ]]; then
        cd $(autojump $@)
        return
    fi
    cd "$(autojump -s | gsed '/_____/Q; s/^[0-9,.:]*\s*//' | fzf --height 40% --reverse --inline-info)"
}

# git interactive rebase with fzf commit selection
girb() {
	git rebase -i $(git log --decorate --oneline --color=always | fzf --ansi | cut -d ' ' -f1 )^
}

# browse chrome history
c() {
  local cols sep google_history open
  cols=$(( COLUMNS / 3 ))
  sep='{::}'

  if [ "$(uname)" = "Darwin" ]; then
    google_history="$HOME/Library/Application Support/Google/Chrome/Default/History"
    open=open
  else
    google_history="$HOME/.config/google-chrome/Default/History"
    open=xdg-open
  fi
  cp -f "$google_history" /tmp/h
  sqlite3 -separator $sep /tmp/h \
    "select substr(title, 1, $cols), url
     from urls order by last_visit_time desc" |
  awk -F $sep '{printf "%-'$cols's  \x1b[36m%s\x1b[m\n", $1, $2}' |
  fzf --ansi --multi | gsed 's#.*\(https*://\)#\1#' | xargs $open > /dev/null 2> /dev/null
}

# Select a docker container to start and attach to
function da() {
  local cid
  cid=$(docker ps -a | sed 1d | fzf -1 -q "$1" | awk '{print $1}')

  [ -n "$cid" ] && docker start "$cid" && docker attach "$cid"
}

# Select a running docker container to stop
function ds() {
  local cid
  cid=$(docker ps | sed 1d | fzf -q "$1" | awk '{print $1}')

  [ -n "$cid" ] && docker stop "$cid"
}

# find in zsh history
fh() {
	print -z $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) | fzf +s --tac | gsed -r 's/ *[0-9]*\*? *//' | gsed -r 's/\\/\\\\/g')
}

export NNN_TMPFILE="/tmp/nnn"
n() {
	nnn "$@"
	if [[ -f $NNN_TMPFILE ]]; then
		. $NNN_TMPFILE
		rm -f $NNN_TMPFILE
	fi
}

unsetopt AUTOcd
setopt noflowcontrol

export GPG_TTY=$(tty)
if [[ -n "$SSH_CONNECTION" ]]; then
	export PINENTRY_USER_DATA="USE_CURSES=1"
fi

[ "$(command -v nvim)" ] && export EDITOR="$(which nvim)"
[ "$(command -v bat)" ] && alias cat="bat"
[ "$(command -v hub)" ] && alias git="hub"
[ "$(command -v hub)" ] && alias gci="hub ci-status -v"
[ "$(command -v exa)" ] && alias ls="exa"
[ "$(command -v nvim)" ] && alias vim="nvim"
[ "$(command -v thefuck)" ] && eval $(thefuck --alias)
[ "$(command -v code-insiders)" ] && alias code="code-insiders"

alias kp="kill_process"
alias bip="brew_install_fzf"
alias dc="docker-compose"
alias gst="git status -s"
alias bid="bundle install --path=vendor --jobs=$(sysctl -n hw.ncpu) --binstubs=.bundle/bin"
alias glog="thicket --color-prefixes | less"
alias gloga="thicket --color-prefixes --all | less"
alias wglog="watch --color -n 1 thicket --color-prefixes -n 200"
alias wgloga="watch --color -n 1 thicket --color-prefixes -n 200 --all"

if [ "$(command -v kitty)" ]; then
	alias aedirnlan="kitty +kitten ssh taylor@aedirn.local"
	alias aedirnwan="kitty +kitten ssh taylor@home.thurlow.io"
	alias cintralan="kitty +kitten ssh taylor@cintra.local"
	alias cintrawan="kitty +kitten ssh taylor@home.thurlow.io -t ssh taylor@cintra.local"
	alias whatbox="kitty +kitten ssh frizkie@apollo.whatbox.ca"
else
	alias aedirnlan="ssh taylor@aedirn.local"
	alias aedirnwan="ssh taylor@home.thurlow.io"
	alias cintralan="ssh taylor@cintra.local"
	alias cintrawan="ssh taylor@home.thurlow.io -t ssh taylor@cintra.local"
	alias feral="ssh frizkie@selene.feralhosting.com"
	alias whatbox="ssh frizkie@apollo.whatbox.ca"
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

get_ruby() {
	$(which ruby) <<RUBY
		$:.unshift File.join(Dir.home, ".dotfiles", "ruby_scripts")
		ARGV = ["$2"]
		$1
RUBY
}

# We want PATH modification to happen even in non-interactive shells, so we'll
# include all the PATH modification in ~/.zprofile. Do not add PATH modification
# to this file.
